;"CONVERT " IS A QUICK WAY TO CONVERT "SCAN" OUTPUT INTO A PROCEDURE
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*  No warranty is expressed or implied.     Written by Dave L Clark *
;*  Neither the author nor the company is    Clarke Industries, Inc. *
;*  responsible for any loss or damage       2100 Highway 265        *
;*  resulting from the use of this code.     Springdale, AR  72764   *
;*  Source is provided on an as-is basis.    Phone (501)750-8248     *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SET  PPDVBL   1
SET  PPDLITEX 1
SET  PPDLPLMT 10000

DCL  TYP CHAR 3 V
SET  TYP 'BIM'

IF SIBSESSD,EQ,0
   EXIT SV,'(CONVERT ) ** COMMAND REQUIRES A MEMBER NAME DISPLAY **'
IF SSDTYPE,NE,DI
   EXIT SV,'(CONVERT ) ** COMMAND REQUIRES A MEMBER NAME DISPLAY **'

TOP
SET  PPDCOND  1
FIND '/CTL/D' 136
IFTHEN SIBRETCD,EQ,OK
   SET  TYP 'VSE'
   GOTO BEGIN
ENDIF
FIND '/CTL/L' 136
IFTHEN SIBRETCD,EQ,OK
   SET  TYP 'LIB'
   GOTO BEGIN
ENDIF
FIND '/CTL/M' 136
IF SIBRETCD,NE,OK
   EXIT SV,'(CONVERT ) ** COMMAND REQUIRES A MEMBER NAME DISPLAY **'

LABEL BEGIN
TOP

DCL  OLIB     CHAR  16 V
DCL  OMEM     CHAR  16 V
DCL  STR      CHAR  80 V
DCL  CMD      CHAR   8 F

PARSE STR CMD
IF CMD,EQ,''
   SET  CMD COPY

GOTO CHECK

LABEL CONVERT
SET  PPDCOND  0
SETD OLIB XTRSLINE 156 16
VTRUNCAT OLIB
IFTHEN TYP NE 'VSE'
  SETD OMEM XTRSLINE   1 16
  VTRUNCAT OMEM
ELSE
  SETD OMEM XTRSLINE   1 8
  VTRUNCAT OMEM
  VAPP OMEM '.'
  VAPP OMEM '&XTRSLINE{10,8}'
  VTRUNCAT OMEM
ENDIF
BL   1-253
OVLY 01 '&CMD'
IFTHEN TYP NE 'LIB'
  OVLY 10 '&OLIB..&OMEM'
ELSE
  OVLY 10 '&OMEM'
ENDIF
OVLY 44 '&STR'

SET  PPDCOND  1
NEXT 1
IF SIBRETCD,NE,OK
   GOTO EXIT

LABEL CHECK
IF SIBSIGNL,EQ,1
   EXIT AT,'## USER INTERRUPTED VIA 3270 ATTENTION KEY ##'
EXAM '&CMD' 1-8
IF SIBRETCD,EQ,OK
   GOTO EXIT
EXAM '_' 1-16
IF SIBRETCD,EQ,OK
   GOTO DELETE
EXAM '/CTL/D' 136-141
IF SIBRETCD,EQ,OK
   GOTO -CONVERT
EXAM '/CTL/L' 136-141
IF SIBRETCD,EQ,OK
   GOTO -CONVERT
EXAM '/CTL/M' 136-141
IF SIBRETCD,EQ,OK
   GOTO -CONVERT

LABEL DELETE
DEL  1
GOTO -CHECK

LABEL EXIT
TOP
EXIT
