;"COMPARE" TWO MEMBERS (OR SESSIONS) UNTIL LINES DIFFER
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*  No warranty is expressed or implied.     Written by Dave L Clark *
;*  Neither the author nor the company is    Clarke Industries, Inc. *
;*  responsible for any loss or damage       2100 Highway 265        *
;*  resulting from the use of this code.     Springdale, AR  72764   *
;*  Source is provided on an as-is basis.    Phone (501)750-8248     *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SET PPDVBL 2
;ET PPDLPLMT 10000
;
DCL RC       CHAR   2 V M
DCL MSG      CHAR  82 V M
DCL STRNG1   CHAR 253 V M
DCL ZONBS1   NUM    3 V
DCL ZONLM1   NUM    3 V
DCL ZONEL1   NUM    3 V
DCL LINES1   NUM    6 V
DCL STRNG2   CHAR 253 V M
DCL ZONBS2   NUM    3 V
DCL ZONLM2   NUM    3 V
DCL ZONEL2   NUM    3 V
DCL LINES2   NUM    6 V
DCL SESSD    NUM    2 V
DCL CREATE   NUM    1 V
;
DCL MEM1     CHAR  33 V U
DCL MEM2     CHAR  33 V U
DCL FCT      NUM    5 V
DCL ZONE     CHAR   7 V U
DCL OPT      CHAR   8 V U
DCL CFCT     NUM    5 V
;
PARSE MEM1 MEM2 FCT ZONE OPT CFCT
IF OPT,EQ,'CONTINUE'
   GOTO CONTINUE
 
IF ZONE,EQ,''
   GOTO NOZONE
SET CREATE 0
IF SIBSESSD,GT,0
   GOTO HAVESESS
DISPLAYF
++/*
SET CREATE 1
LABEL HAVESESS
SETD ZONBS1 SSDZONBS
SETD ZONLM1 SSDZONLM
SET PPDCOND 2
SESS ZONE=&ZONE
SETD MSG SIBRESP
SETD RC MSG 1 2
SETD MSG MSG 3
SET PPDCOND 0
SESS ZONE=&ZONBS1-&ZONLM1
IF CREATE,EQ,1
   END
IF RC,NE,OK
   EXIT &RC,'&MSG'
LABEL NOZONE
 
IF FCT,EQ,0
   SET FCT 99999
SETD CFCT FCT
 
IF MEM1,EQ,''
   GOTO CONTINUE
IF MEM2,EQ,''
   EXIT SV,'(COMPARE ) ** COMMAND REQUIRES TWO MEMBERS TO COMPARE **'
SET  SESSD &SIBSSDMX
SUBT SESSD &SIBSESSU
IF SESSD,LT,2
   EXIT SV,'(COMPARE ) ** COMMAND REQUIRES TWO FREE SESSIONS **'
 
READ &MEM1
READ &MEM2
 
SCREEN SPLIT OFF
LIST &MEM1
NEXT 1
SCREEN SPLIT ON
LIST &MEM2
NEXT 1
 
LABEL CONTINUE
SET PA1 ''
IF SIBLSCDP,NE,2
   EXIT SV,'(COMPARE ) ** COMMAND REQUIRES TWO MEMBERS TO COMPARE **'
 
IF SSDBASE&SIBSCRPM,GT,0
   UP 1
SETD ZONBS&SIBSCRPM SSDZONBS
SETD ZONLM&SIBSCRPM SSDZONLM
IF ZONE,NE,''
   SESS ZONE=&ZONE
SETD ZONEL&SIBSCRPM SSDZONLM
SUBT ZONEL&SIBSCRPM &SSDZONBS
ADD  ZONEL&SIBSCRPM 1
<
IF SSDBASE&SIBSCRPM,GT,0
   UP 1
SETD ZONBS&SIBSCRPM SSDZONBS
SETD ZONLM&SIBSCRPM SSDZONLM
IF ZONE,NE,''
   SESS ZONE=&ZONE
SETD ZONEL&SIBSCRPM SSDZONLM
SUBT ZONEL&SIBSCRPM &SSDZONBS
ADD  ZONEL&SIBSCRPM 1
 
IF SIBSCRPM,NE,1
   <
SET PPDCOND 2
 
LABEL COMPARE
IF CFCT,EQ,0
   GOTO RESET
<
EXAMINE '"'
IF SSDBASE&SIBSCRPM,EQ,&SSDSZLN
   GOTO RESET
NEXT 1
SETD STRNG&SIBSCRPM XTRSLINE &SSDZONBS &ZONEL&SIBSCRPM
<
EXAMINE '"'
IF SSDBASE&SIBSCRPM,EQ,&SSDSZLN
   GOTO RESET
NEXT 1
SETD STRNG&SIBSCRPM XTRSLINE &SSDZONBS &ZONEL&SIBSCRPM
 
SUBTRACT CFCT 1
 
IFD STRNG1,EQ,STRNG2
   GOTO -COMPARE
 
LABEL MISMATCH
<
SESS ZONE=&ZONBS&SIBSCRPM-&ZONLM&SIBSCRPM
<
SESS ZONE=&ZONBS&SIBSCRPM-&ZONLM&SIBSCRPM
SET PA1,"COMPARE FCT=&FCT ZONE='&ZONE' OPT=CONTINUE CFCT=&CFCT"
EXIT OK,'(COMPARE ) ** MISMATCH AT CURRENT LINES, RE-ALIGN & HIT "PA1" TO CONTINUE **'
 
LABEL RESET
<
SESS ZONE=&ZONBS&SIBSCRPM-&ZONLM&SIBSCRPM
<
SESS ZONE=&ZONBS&SIBSCRPM-&ZONLM&SIBSCRPM
 
IF CFCT,EQ,0
   EXIT OK,'(COMPARE ) ** &FCT LINES COMPARED - COMPARE COMPLETE **'
 
IF SIBSCRPM,EQ,1
   <
EXAMINE '"'
SETD LINES&SIBSCRPM SSDSZLN
SUBT LINES&SIBSCRPM &SSDBASE&SIBSCRPM
<
EXAMINE '"'
SETD LINES&SIBSCRPM SSDSZLN
SUBT LINES&SIBSCRPM &SSDBASE&SIBSCRPM
 
IF LINES2,LT,&LINES1
   <
IF LINES1,NE,&LINES2
   EXIT OK,'(COMPARE ) ** REACHED END OF SESSION &SIBSCRPM - COMPARE MISMATCH **'
EXIT OK,'(COMPARE ) ** REACHED END OF SESSIONS - COMPARE COMPLETE **'
