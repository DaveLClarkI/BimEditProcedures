;"JCL" IS A NEW COMMAND FOR THE EDITOR
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
;*  No warranty is expressed or implied.     Written by Dave L Clark *
;*  Neither the author nor the company is    Clarke Industries, Inc. *
;*  responsible for any loss or damage       2100 Highway 265        *
;*  resulting from the use of this code.     Springdale, AR  72764   *
;*  Source is provided on an as-is basis.    Phone (501)750-8248     *
;* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
SET  PPDVBL   1

IF SIBSESSD,EQ,0
   EXIT SV,'(JCL     ) ** COMMAND REQUIRES A SESSION **'

IF SSDTYPE,EQ,DI
   GOTO OK
IF SSDTYPEM,EQ,J
   GOTO OK
IF SSDTYPEM,EQ,Z
   GOTO OK
IF SSDTYPEM,NE,JCL
   EXIT SV,'(JCL     ) ** COMMAND REQUIRES "JCL" MEMBER TYPE **'

LABEL OK
IF SSDTYPE,NE,DI
   READ &SSDLIB..&SSDMEM

DCL  A        CHAR   8 V
DCL  L        NUM    3 V
DCL  N        NUM    3 V
DCL  P        NUM    3 V
DCL  X        NUM    6 V
DCL  DESC     CHAR  40 V
DCL  JOBDATA  CHAR  62 V
DCL  LSTDATA  CHAR  62 V
DCL  PUNDATA  CHAR  62 V
DCL  ASTER    CHAR   1 V
DCL  DLRDLR   CHAR   2 V
;
DCL  JNM      CHAR   8 V
DCL  DISP     CHAR   1 V
DCL  CLASS    CHAR   1 V
DCL  PRI      CHAR   1 V
DCL  COPY     NUM    3 V
DCL  XDEST    CHAR  19 V
DCL  LDEST    CHAR  19 V
DCL  PDEST    CHAR  19 V
DCL  DEST     CHAR  19 V
DCL  USER     CHAR  16 V
DCL  LST      CHAR   6 V
DCL  FCB      CHAR   8 V
DCL  FNO      CHAR   4 V
DCL  JSEP     NUM    1 V
DCL  REMOTE   NUM    3 V
DCL  RSL      CHAR   6 V
DCL  PUN      CHAR   6 V
;
DCL  JOBDISP  CHAR   1 V
DCL  JOBCLASS CHAR   1 V
DCL  JOBPRI   CHAR   1 V
DCL  JOBDEST  CHAR  19 V
DCL  JOBUSER  CHAR  16 V
DCL  LSTDISP  CHAR   1 V
DCL  LSTCLASS CHAR   1 V
DCL  LSTCOPY  NUM    3 V
DCL  LSTDEST  CHAR  19 V
DCL  LSTFCB   CHAR   8 V
DCL  LSTFNO   CHAR   4 V
DCL  LSTREMT  NUM    3 V
DCL  LSTRSL   CHAR   6 V
DCL  PUNDISP  CHAR   1 V
DCL  PUNCLASS CHAR   1 V
DCL  PUNCOPY  NUM    3 V
DCL  PUNDEST  CHAR  19 V
DCL  PUNFNO   CHAR   4 V
DCL  PUNREMT  NUM    3 V

IF SSDSZLN,EQ,0
   GOTO INSERT

POSITION &SSDSZLN

IF XTRSLINE,EQ,''
   DELETE 1
IF XTRSLINE,EQ,''
   DELETE 1
LABEL BOTLOOP
SETD A        XTRSLINE 1 3
IF A,EQ,'* $'
   GOTO BOTDLET
IF A,EQ,'/& '
   GOTO BOTDLET
IF A,EQ,'/* '
   GOTO BOTDLET
GOTO NEXT
LABEL BOTDLET
DELETE 1
GOTO -BOTLOOP

LABEL NEXT
TOP
NEXT 1

LABEL TOPLOOP
SETD A        XTRSLINE 1 6
IF A,EQ,'* $$ J'
   GOTO JOBDATA
IF A,EQ,'* $$ L'
   GOTO LSTDATA
IF A,EQ,'* $$ P'
   GOTO PUNDATA
IF A,EQ,'// JOB'
   GOTO CONTINUE
GOTO INSERT

LABEL JOBDATA
IF JOBDATA,NE,''
   GOTO TOPDLET
SETD PARMLIST XTRSLINE 1 72
SET  DISP     ''
SET  CLASS    ''
PARSE ASTER DLRDLR JOBDATA JNM DISP CLASS PRI XDEST USER LDEST PDEST
IF DISP,NE,''
   SETD JOBDISP DISP
IF CLASS,NE,''
   SETD JOBCLASS CLASS
IF PRI,NE,''
   SETD JOBPRI PRI
IF USER,NE,''
   SETD JOBUSER USER
IF XDEST,NE,''
   SETD JOBDEST XDEST
IF LDEST,NE,''
   SETD LSTDEST LDEST
IF PDEST,NE,''
   SETD PUNDEST PDEST
GOTO TOPDLET

LABEL LSTDATA
IF LSTDATA,NE,''
   GOTO TOPDLET
SETD PARMLIST XTRSLINE 1 72
SET  DISP     ''
SET  CLASS    ''
SET  COPY     0
SET  REMOTE   0
SET  DEST     ''
SET  FNO      ''
PARSE ASTER DLRDLR LSTDATA LST DISP CLASS COPY PRI DEST FCB FNO JSEP REMOTE RSL
IF DISP,NE,''
   SETD LSTDISP DISP
IF CLASS,NE,''
   SETD LSTCLASS CLASS
IF COPY,NE,0
   SETD LSTCOPY COPY
IF DEST,NE,''
   SETD LSTDEST DEST
IF FCB,NE,''
   SETD LSTFCB FCB
IF FNO,NE,''
   SETD LSTFNO FNO
IF REMOTE,NE,0
   SETD LSTREMT REMOTE
IF RSL,NE,''
   SETD LSTRSL RSL
GOTO TOPDLET

LABEL PUNDATA
IF PUNDATA,NE,''
   GOTO TOPDLET
SETD PARMLIST XTRSLINE 1 72
SET  DISP     ''
SET  CLASS    ''
SET  COPY     0
SET  REMOTE   0
SET  DEST     ''
SET  FNO      ''
PARSE ASTER DLRDLR PUNDATA PUN DISP CLASS COPY PRI DEST FNO JSEP REMOTE
IF DISP,NE,''
   SETD PUNDISP DISP
IF CLASS,NE,''
   SETD PUNCLASS CLASS
IF COPY,NE,0
   SETD PUNCOPY COPY
IF DEST,NE,''
   SETD PUNDEST DEST
IF FNO,NE,''
   SETD PUNFNO FNO
IF REMOTE,NE,0
   SETD PUNREMT REMOTE
LABEL TOPDLET
DELETE 1
GOTO -TOPLOOP

LABEL CONTINUE               ;EXTRACT DESCRIPTION FROM JOB CARD
SETD PARMLIST XTRSLINE 1 71
SET  N        0
LABEL START
ADD  N        1
SETL L        PARMLIST
SET  P        0
LABEL LOOP
ADD  P        1
IF P,GT,&L
   GOTO DELETE
SETD A        PARMLIST &P 1
IF A,NE,' '
   GOTO -LOOP
LABEL LOOP2
ADD  P        1
IF P,GT,&L
   GOTO DELETE
SETD A        PARMLIST &P 1
IF A,EQ,' '
   GOTO -LOOP2
SETD PARMLIST PARMLIST &P
IF N,LT,3
   GOTO -START
SETD DESC     PARMLIST 1 40
LABEL DELETE
DELETE 1

LABEL INSERT
IF DESC,EQ,''
   SETD DESC TXMTITLE
IF DESC,EQ,''
   SET DESC '<=== SPECIFY 40 CHAR JOB DESCRIPTION ==>'
IF SSDTYPEM,NE,JCL
   GOTO BEGIN
IF TXMTITLE,NE,''
   GOTO BEGIN
SET  PPDLITEX 1
SET  PPDCOND  2
ALTER &TXMLIB..&TXMMEM,TITLE='&DESC'
SET  PPDCOND  0
SET  PPDLITEX 0

LABEL BEGIN
SET  JOBDATA  'JNM=&SSDMEM'
IF JOBDISP,EQ,''
   SET  JOBDISP  D
SET  JOBDATA  '&JOBDATA.,DISP=&JOBDISP'
IF JOBCLASS,EQ,''
   SET  JOBCLASS C
SET  JOBDATA  '&JOBDATA.,CLASS=&JOBCLASS'
IF JOBPRI,NE,''
   SET  JOBDATA  '&JOBDATA.,PRI=&JOBPRI'
IF JOBDEST,NE,''
   SET  JOBDATA  '&JOBDATA.,XDEST=&JOBDEST'
IF JOBUSER,EQ,''
   SET  JOBUSER  &SIBUSER
SET  JOBDATA  "&JOBDATA.,USER='&JOBUSER'"

SET  LSTDATA  'LST=SYSLST'
IF LSTDISP,EQ,''
   SET  LSTDISP  D
SET  LSTDATA  '&LSTDATA.,DISP=&LSTDISP'
IF LSTCLASS,EQ,''
   SET  LSTCLASS Z
SET  LSTDATA  '&LSTDATA.,CLASS=&LSTCLASS'
IF LSTCOPY,GT,1
   SET  LSTDATA  '&LSTDATA.,COPY=&LSTCOPY'
IF LSTDEST,NE,''
   SET  LSTDATA  '&LSTDATA.,DEST=&LSTDEST'
IF LSTFCB,EQ,$$BFCB08
   SET  LSTFCB   ''
IF LSTFCB,NE,''
   SET  LSTDATA  '&LSTDATA.,FCB=&LSTFCB'
IF LSTFNO,NE,''
   SET  LSTDATA  '&LSTDATA.,FNO=&LSTFNO'
IF LSTREMT,GT,0
   SET  LSTDATA  '&LSTDATA.,REMOTE=&LSTREMT'
IF LSTRSL,NE,''
   SET  LSTDATA  '&LSTDATA.,RSL=&LSTRSL'

SET  PUNDATA  'PUN=SYSPCH'
IF PUNDISP,EQ,''
   SET  PUNDISP  D
SET  PUNDATA  '&PUNDATA.,DISP=&PUNDISP'
IF PUNCLASS,EQ,''
   SET  PUNCLASS Z
SET  PUNDATA  '&PUNDATA.,CLASS=&PUNCLASS'
IF PUNCOPY,GT,1
   SET  PUNDATA  '&PUNDATA.,COPY=&PUNCOPY'
IF PUNDEST,NE,''
   SET  PUNDATA  '&PUNDATA.,DEST=&PUNDEST'
IF PUNFNO,NE,''
   SET  PUNDATA  '&PUNDATA.,FNO=&PUNFNO'
IF PUNREMT,GT,0
   SET  PUNDATA  '&PUNDATA.,REMOTE=&PUNREMT'

TOP
INSERTF
* $$ JOB &JOBDATA
* $$ LST &LSTDATA
* $$ PUN &PUNDATA
// JOB    &SSDMEM   &DESC
++/*

POSITION &SSDSZLN
INSERTF
/* EOD
/& EOJ
* $$ EOJ
++/*

TOP
NEXT 1
EXIT
