* -------------------------------------------------------------------- *
* PROCEDURE: CHECKOUT
* TITLE....: MOVE MEMBERS INTO TEST STATUS
* AUTHOR...: CLARK WALKER                     CREATED: 03/07/85
* AUTHOR...: DAVE L CLARK               MAJOR REWRITE: 11/05/2001
* -------------------------------------------------------------------- *
SET  PPDVBL,1
DCL  LSTMEM  CHAR 33 V       ;TEMPORARY HOLDER FOR LAST REFERENCED INFO
SETD LSTMEM  TXMPATH         ;SAVE THE USER'S LAST REFERENCED INFO

DCL  D       CHAR  1 V
DCL  ATT     CHAR  1 V
DCL  DMY1    CHAR  6 V
DCL  DMY2    CHAR  8 V
DCL  BY      CHAR 18 V
DCL  CREATE  CHAR 20 V
DCL  INTO    CHAR 33 V
DCL  FROM    CHAR 33 V
DCL  TSTLIB  CHAR 16 V
DCL  PRDLIB  CHAR 16 V
DCL  MEMNAM  CHAR 16 V

IF PARMLIST,EQ,''
  EXIT ER,'(CHECKOUT) ** Please specify a LIBRARY.MEMBER name'
DCL  NAME    CHAR 33 V
PARSE NAME

SET PPDCOND,1
READ &NAME
IFTHEN SIBRETCD,NE,OK
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKOUT) ** Member &NAME not found'
ENDIF
IFTHEN XTREUSER NE ''
  SET  DMY2 &XTREUSER
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKOUT) ** Member &NAME is being edited by "&DMY2".'
ENDIF
IFTHEN XTRLUSER NE ''
  SET  DMY2 &XTRLUSER
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKOUT) ** Member &NAME is being listed by "&DMY2".'
ENDIF
SET  PPDCOND   0

SET  ATT 'N'
IF SIBLIB,LE,' '
   SET  ATT 'Y'
IF SIBUSER,EQ,'DLC'
   SET  ATT 'Y'

IFTHEN ATT,EQ,'Y'
  IFTHEN TXMTYPE,EQ,'JCL'
    ATTACH &SIBUSER..JCL
  ELSE
    IFTHEN TXMTYPE,EQ,'JOBMODEL'
      ATTACH &SIBUSER..JOB
    ELSE
      ATTACH &SIBUSER..PRO
    ENDIF
  ENDIF
ENDIF

SET  PPDCOND   1
SET  TSTLIB &TXMLIB
SET  PRDLIB &TXMLIB
SET  MEMNAM &TXMMEM
BYPASS
LIST &PRDLIB..&MEMNAM
EXAMINE '*CHKO' 1-5
IFTHEN SIBRETCD,EQ,OK
  SETD PARMLIST XTRSLINE
  BYPASS
  END NOSAVE
  PARSE DMY1 DMY2 CREATE BY FROM
  READ &FROM
  IFTHEN SIBRETCD,NE,OK
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKOUT) ** Production member &FROM not found'
  ENDIF
  IFTHEN XTREUSER NE ''
    SET  DMY2 &XTREUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKOUT) ** Production member &FROM is being edited by "&DMY2".'
  ENDIF
  IFTHEN XTRLUSER NE ''
    SET  DMY2 &XTRLUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKOUT) ** Production member &FROM is being listed by "&DMY2".'
  ENDIF
  SET  TSTLIB &PRDLIB
  SET  PRDLIB &TXMLIB
  SET  MEMNAM &TXMMEM
  BYPASS
  LIST &PRDLIB..&MEMNAM
ENDIF
EXAMINE '%CHKO' 1-5
IF SIBRETCD EQ 'OK'
  GOTO INTEST

READ &MEMNAM
IF SIBRETCD NE 'OK'
  GOTO READY

BYPASS
END NOSAVE
SET  PPDCOND,2               ;BYPASS ALL ERRORS
READ &LSTMEM                 ;RESTORE USER'S LAST REFERENCED INFO
SET  PPDCOND,0               ;DEFAULT ERROR HANDLING
EXIT ER,'(CHECKOUT) ** Member "&SIBLIB..&MEMNAM" already exists!'

LABEL INTEST
  SET PPDCOND 0
  SETD PARMLIST XTRSLINE
  PARSE DMY1 DMY2 CREATE BY INTO

  SET  D V
  MAPF


         This member exists as: &INTO
                          user: &BY
                         since: &CREATED

         Do you wish to 'R'emove the member from checkout status?
         ...or, 'V'iew the production member (R/V)? {D}


++/*
  IF D,EQ,R
    GOTO REMOVE
  EXIT ER,'(CHECKOUT) ** Member already in CHECKOUT status, see below:'

LABEL REMOVE
  BYPASS
  END NOSAVE
  SET PPDCOND 0
  BYPASS
  PURGE &INTO
  BYPASS
  EDIT &PRDLIB..&MEMNAM
  TOP
  DEL 1
  BYPASS
  SAVE
  BYPASS
  EDIT COMMON.TESTPROD
  BOTTOM
  FORWARD
  INSERTF
%CHKO MEMBER &INTO REMOVED BY &SIBUSER
++/*
  BYPASS
  SAVE
  EXIT OK,'(CHECKOUT) ** Member removed from CHECKOUT status.'

LABEL READY
  SET PPDCOND 0
  BYPASS
  END NOSAVE
  IFTHEN PRDLIB EQ '&SIBLIB'
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKOUT) ** Production and test (attached) libraries cannot be the same'
  ENDIF

  COPY &PRDLIB..&MEMNAM &MEMNAM
  BYPASS
  EDIT &PRDLIB..&MEMNAM
  TOP
  INSERTF
%CHKO %VERSION CREATE=&OPSDATE@&OPSTIME BY=&SIBUSER INTO=&SIBLIB..&MEMNAM
++/*
  BYPASS
  QUIT
 ;ALT &MEMNAM AUDIT=OFF      ;STOP CLEARING AUDIT TRAIL
  ALT &MEMNAM AUDIT=ON STAMP=ON

  BYPASS
  EDIT COMMON.TESTPROD
  BOTTOM
  FORWARD
  INSERTF
%CHKO %VERSION CREATE=&OPSDATE@&OPSTIME BY=&SIBUSER INTO=&SIBLIB..&MEMNAM
++/*
  BYPASS
  QUIT

  EDIT &MEMNAM
  TOP
  INSERTF
*CHKO *VERSION CREATE=&OPSDATE@&OPSTIME BY=&SIBUSER FROM=&PRDLIB..&MEMNAM
++/*
  BYPASS
  SAVE
  EDIT &MEMNAM

EXIT OK,'(CHECKOUT) ** Member "&PRDLIB..&MEMNAM" copied to "&SIBLIB" with AUDIT=ON.'
