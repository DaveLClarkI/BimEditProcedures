* -------------------------------------------------------------------- *
* PROCEDURE: CHECKIN
* TITLE....: MOVE MEMBERS INTO PRODUCTION STATUS
* AUTHOR...: CLARK WALKER                     CREATED: 03/07/85
* AUTHOR...: DAVE L CLARK               MAJOR REWRITE: 02/21/2002
* -------------------------------------------------------------------- *
SET  PPDVBL,1
DCL  LSTMEM  CHAR 33 V       ;TEMPORARY HOLDER FOR LAST REFERENCED INFO
SETD LSTMEM  TXMPATH         ;SAVE THE USER'S LAST REFERENCED INFO

DCL  D       CHAR  1
DCL  DMY1    CHAR  6 V
DCL  DMY2    CHAR  8 V
DCL  BY      CHAR 18 V
DCL  CREATE  CHAR 20 V
DCL  CREATED CHAR 20 V
DCL  FROM    CHAR 33 V
DCL  INTO    CHAR 33 V
DCL  N       NUM   4 V
DCL  PRDLIB  CHAR 16 V
DCL  TSTLIB  CHAR 16 V
DCL  MEMNAM  CHAR 16 V
DCL  MEMTYP  CHAR  8 V
DCL  MEMTTL  CHAR 40 V
DCL  OBSNAM  CHAR 16 V
DCL  OBSMSG  CHAR 34 V
SET  OBSMSG  'no prior version was present.'

IF PARMLIST,EQ,''
  EXIT ER,'(CHECKIN) ** Please specify a LIBRARY.MEMBER name'
DCL  NAME   CHAR 33 V
DCL  CHECK  CHAR  8 V
PARSE NAME CHECK

SET PPDCOND,1
READ &NAME
IFTHEN SIBRETCD,NE,OK
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKIN) ** Member &NAME not found'
ENDIF
IFTHEN XTREUSER NE ''
  SET  DMY2 &XTREUSER
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKIN) ** Member &NAME is being edited by "&DMY2".'
ENDIF
IFTHEN XTRLUSER NE ''
  SET  DMY2 &XTRLUSER
  SET  PPDCOND,2             ;BYPASS ALL ERRORS
  READ &LSTMEM               ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0             ;DEFAULT ERROR HANDLING
  EXIT ER,'(CHECKIN) ** Member &NAME is being listed by "&DMY2".'
ENDIF

SET  TSTLIB &TXMLIB
SET  PRDLIB &TXMLIB
SET  MEMNAM &TXMMEM
SET  MEMTYP &TXMTYPE
SET  PPDLITEX 1
SET  MEMTTL '&TXMTITLE'

BYPASS
LIST &PRDLIB..&MEMNAM
EXAMINE '\\*CHKO|\*TEST' 1-5
IFTHEN SIBRETCD,EQ,OK
  SETD PARMLIST XTRSLINE
  BYPASS
  END NOSAVE
  VEXAM PARMLIST 'CREATED' 16-22
  IFTHEN SIBRETCD,EQ,OK
    PARSE DMY1 DMY2 CREATED BY FROM
  ELSE
    PARSE DMY1 DMY2 CREATE BY FROM
  ENDIF
  READ &FROM
  IFTHEN SIBRETCD,NE,OK
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Production member &FROM not found'
  ENDIF
  IFTHEN XTREUSER NE ''
    SET  DMY2 &XTREUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Production member &FROM is being edited by "&DMY2".'
  ENDIF
  IFTHEN XTRLUSER NE ''
    SET  DMY2 &XTRLUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Production member &FROM is being listed by "&DMY2".'
  ENDIF
  SET  TSTLIB &PRDLIB
  SET  PRDLIB &TXMLIB
  SET  MEMNAM &TXMMEM
  LIST &PRDLIB..&MEMNAM
ENDIF
EXAMINE '\\%CHKO|\%TEST' 1-5
IF SIBRETCD,EQ,OK
   GOTO NOVIOL1

BYPASS
END NOSAVE
BYPASS
ED COMMON.TESTPROD
POS &SSDSZLN
INSERTF
%CHKI SECURITY &OPSDATE-&SIBUSER PROD &MEMNAM FROM &TSTLIB (NO %CHKO)
++/*
BYPASS
SAVE

SET  PPDCOND,2               ;BYPASS ALL ERRORS
READ &LSTMEM                 ;RESTORE USER'S LAST REFERENCED INFO
SET  PPDCOND,0               ;DEFAULT ERROR HANDLING
EXIT ER,'(CHECKIN) ** "CHECKOUT" was not done on this member originally!'

LABEL NOVIOL1
  SETD PARMLIST XTRSLINE
  BYPASS
  END NOSAVE
  VEXAM PARMLIST 'CREATED' 16-22
  IFTHEN SIBRETCD,EQ,OK
    PARSE DMY1 DMY2 CREATED BY INTO
  ELSE
    PARSE DMY1 DMY2 CREATE BY INTO
  ENDIF
  READ &INTO
  IFTHEN SIBRETCD,NE,OK
    SET PPDCOND,2            ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET PPDCOND,0            ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Checkout member &INTO not found'
  ENDIF
  IFTHEN XTREUSER NE ''
    SET  DMY2 &XTREUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Checkout member &INTO is being edited by "&DMY2".'
  ENDIF
  IFTHEN XTRLUSER NE ''
    SET  DMY2 &XTRLUSER
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT ER,'(CHECKIN) ** Checkout member &INTO is being listed by "&DMY2".'
  ENDIF
  SETD TSTLIB TXMLIB
  SETD MEMNAM TXMMEM

  SET OBSMSG,'prior OBSOLETE left.'
  SET OBSNAM,'&MEMNAM-&TXMTYPE'
  READ OBSOLETE.&OBSNAM
  IF SIBRETCD,EQ,OK
     GOTO OBSEXIST
  SET  D 'C'
  SET OBSMSG,'OBSOLETE created.'
  GOTO TESTCOMP

LABEL OBSEXIST
  SET PPDCOND 0
  READ &PRDLIB..&MEMNAM

  SET  D 'O'
  MAPF


         THE MEMBER EXISTS AS OBSOLETE.&OBSNAM

         DO YOU WISH TO DELETE THE OBSOLETE MEMBER  (normal)
           OR THE PRODUCTION MEMBER (O/P)? {D}


++/*

  IF D,EQ,'O'
    GOTO TESTCOMP
  IF D,EQ,'P'
    GOTO TESTCOMP
  IFTHEN PPDKEY EQ 'CLEAR'
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT OK,'(CHECKIN) ** Operation cancelled.'
  ENDIF
  GOTO -OBSEXIST

LABEL TESTCOMP
  IF  CHECK,EQ,'NOCHECK'
    GOTO TESTSKIP
  SET  PPDCOND  1
  BYPASS
  PURGEP &MEMNAM
  IF SIBRETCD,EQ,OK
    GOTO -TESTCOMP
  SET  PPDCOND  0
  COMPILE &TSTLIB..&MEMNAM LINK=NOLINK
  SET  PPDCOND  1
LABEL CHKLSTNG
  SLEEP 1
  BYPASS
  LISTP
  IF SIBRETCD,NE,OK
    GOTO -CHKLSTNG
  BYPASS
  FIND '* COMPILER RC='
  IF SIBRETCD,NE,OK
    GOTO LSTNGOK
  SET N &XTRSLINE{15,4}
  IFTHEN N GT 4
    BYPASS
    QUIT
    SET  PPDCOND,2           ;BYPASS ALL ERRORS
    READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
    SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
    EXIT OK,'(CHECKIN) ** Test compile rc > 4; CHECKIN blocked **'
  ENDIF
  IF TXMTYPE,NE,RPGII
    GOTO LSTNGOK
  BYPASS
  TOP
  BYPASS
  LOCATE 'C O M P I L E R'
  IF SIBRETCD,NE,OK
    GOTO LSTNGOK
  BYPASS
  LOCATE 'DROPPED.'
  IF SIBRETCD,NE,OK
    GOTO LSTNGOK
  BYPASS
  QUIT
  SET  PPDCOND,2           ;BYPASS ALL ERRORS
  READ &LSTMEM             ;RESTORE USER'S LAST REFERENCED INFO
  SET  PPDCOND,0           ;DEFAULT ERROR HANDLING
  EXIT OK,'(CHECKIN) ** Test compile failed; CHECKIN blocked **'

LABEL LSTNGOK
  BYPASS
  QUIT

LABEL TESTSKIP
  IF D,EQ,'C'
    GOTO CREATEOBS
  IF D,EQ,'P'
    GOTO PURGEPRD

LABEL PURGEOBS
  SET  PPDCOND  0
 ;PURGE OBSOLETE.&OBSNAM     ;INSTEAD OF PURGING IT, ARCHIVE IT
  ARCHIVE OBSOLETE.&OBSNAM REF=&PRDLIB..&MEMNAM
  SET OBSMSG,'OBSOLETE archived and recreated.'
LABEL CREATEOBS
  SET  PPDCOND  0
  READ &PRDLIB..&MEMNAM
  RENAME &PRDLIB..&MEMNAM OBSOLETE.&OBSNAM
  ALT OBSOLETE.&OBSNAM TITLE='MOVED FROM &PRDLIB BY &SIBUSER'
  DCL CURDATE CHAR 8 V
  SET CURDATE '&OPSDATE{1,6}&OPSDATE{9,2}'
  ALT OBSOLETE.&OBSNAM TYPE='&CURDATE' USER=&SIBUSER
  GOTO TSTPRD

LABEL PURGEPRD
  SET  PPDCOND  0
  PURGE &PRDLIB..&MEMNAM
  SET OBSMSG,'prior OBSOLETE left.'

LABEL TSTPRD
  SET  PPDCOND  0
  RENAME &TSTLIB..&MEMNAM &PRDLIB..&MEMNAM
  ALTER &PRDLIB..&MEMNAM USER=&SIBUSER
  DCL OLDYEAR NUM   4 F
  DCL OLDDATE CHAR 10 V
  SET OLDYEAR &OPSDATE{7,4}
  SUBT OLDYEAR 1
  SET OLDDATE '&OPSDATE{1,3}01/&OLDYEAR'
  AUDITCL &PRDLIB..&MEMNAM DATE=&OLDDATE
  BYPASS
  ED &PRDLIB..&MEMNAM
  SET  PPDCOND  1
  EXAMINE '\\*CHKO|\*TEST' 1-5
  IF SIBRETCD,EQ,OK
     DELETE 1
  SET  PPDCOND  0
  BYPASS
  SAVE
  BYPASS
  ED COMMON.TESTPROD
  POS &SSDSZLN
  INSERTF
%CHKI MEMBER &TSTLIB..&MEMNAM INTO PROD LIBRARY &PRDLIB &OBSMSG BY &SIBUSER
++/*
  BYPASS
  QUIT
  READ &PRDLIB..&MEMNAM

LABEL PRODCOMP
  SET  PPDCOND  2
  PCOMPILE
  IF SIBRETCD,EQ,OK
     GOTO DONE
  SLEEP
  GOTO -PRODCOMP

LABEL DONE
SET  PPDCOND  0
EXIT OK,'(CHECKIN) ** &TSTLIB..&MEMNAM renamed into &PRDLIB and &OBSMSG'
