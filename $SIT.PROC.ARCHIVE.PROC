* -------------------------------------------------------------------- *
* PROCEDURE: ARCHIVE
* TITLE....: SEND A MEMBER AND ITS AUDIT TRAIL TO THE NETWORK
* AUTHOR...: DAVE L CLARK                     CREATED: 03/08/2016
* -------------------------------------------------------------------- *
SET  PPDVBL,1                ;PERFORM VARIABLE SUBSTITUTION

DCL  DMY1    CHAR  6 V       ;WORKING VARIABLE DEFINITIONS
DCL  DMY2    CHAR  8 V
DCL  ARCTIME CHAR  6 V
DCL  ARCDATE CHAR  8 V
DCL  ARCMEM  CHAR 16 V
DCL  ARCTMP  CHAR 16 V
DCL  ARCAUD  CHAR 16 V
DCL  LEN     NUM   3 V
DCL  LIBNAM  CHAR 16 V
DCL  MEMNAM  CHAR 16 V
DCL  MEMTYP  CHAR  8 V
DCL  MEMTTL  CHAR 50 V

DCL  R       CHAR  1 INIT=Y  ;USER RESPONSE
DCL  MESG    CHAR 78 V U

DCL  MEM     CHAR 33 V       ;PARAMETER VARIABLE DEFINITIONS
DCL  NAME    CHAR  8 V
DCL  TYPE    CHAR  8 V
DCL  TITLE   CHAR 40 V
DCL  REF     CHAR 33 V
PARSE MEM NAME TYPE TITLE REF
IF MEM,EQ,''
  EXIT ER,'(ARCHIVE) ** PARAMETER "MEM" IS REQUIRED **'
IF MEM,EQ,'*'
  SET  MEM &TXMPATH
IFTHEN MEM,EQ,'/'
  SET  PPDCOND  1
  VEXAM SSDTYPE '\ED|LI|DI'
  IF SIBRETCD,NE,OK
    EXIT ER,'(ARCHIVE) ** CANNOT ARCHIVE &SSDTYPE SESSION **'
  SET  PPDCOND  0
  SET  MEM &SSDLIB..&SSDMEM
  QUIT
ENDIF

SET  PPDCOND,1               ;ALLOW PROCEDURE TO DO ERROR CHECKING
READ &MEM                    ;GET MEMBER ATTRIBUTES -- IF EXISTS
IFTHEN SIBRETCD,NE,OK
  EXIT ER,'(ARCHIVE) ** MEMBER "&MEM" NOT FOUND **'
ENDIF
IFTHEN XTREUSER,NE,''
  SET  DMY2 &XTREUSER
  EXIT ER,'(ARCHIVE) ** MEMBER "&MEM" IS BEING EDITED BY USER "&DMY2" **'
ENDIF
IFTHEN XTRLUSER,NE,''
  SET  DMY2 &XTRLUSER
  EXIT ER,'(ARCHIVE) ** MEMBER "&MEM" IS BEING LISTED BY USER "&DMY2" **'
ENDIF
SET  PPDCOND,0               ;PERFORM DEFAULT ERROR HANDLING

IF SIBDIALG,NE,T
   GOTO CONTINUE
IF SIBOUTPT,NE,D
   GOTO CONTINUE

SET  PPDSCRSZ  D
MAPF


}         About to archive¬&TXMLIB..&TXMMEM}...

}         This will remove the member from BIM-EDIT and
}         write it to the mainframe BIM-EDIT archive
}         folder on the network.

}         Are you sure you want to do this? {R} (Y or N)












}Enter your response and press¬ENTER}to begin processing.
¬PF3}= Exit    ¬PF12}= Cancel
¬&MESG
++/*

IF PPDKEY EQ 'CLEAR'
   EXIT ER,'(ARCHIVE) ** OPERATION CANCELLED BY USER **'
IF PPDKEY EQ 'PF12'
   EXIT ER,'(ARCHIVE) ** OPERATION CANCELLED BY USER **'
IF PPDKEY EQ 'PF3'
   EXIT ER,'(ARCHIVE) ** OPERATION CANCELLED BY USER **'
IF R,NE,Y
   EXIT ER,'(ARCHIVE) ** &TXMLIB..&TXMMEM ARCHIVE BYPASSED **'

LABEL CONTINUE

SET  LIBNAM  &TXMLIB         ;SAVE SELECTED ATTRIBUTES
SET  MEMNAM  &TXMMEM
SET  ARCMEM  &TXMMEM
SET  ARCAUD  &TXMMEM._AUD

IFTHEN TXMDTUPD,GT,''        ;IF AN UPDATE DATE IS PRESENT
  SET  ARCTIME '&TXMTMUPD{1,2}&TXMTMUPD{4,2}&TXMTMUPD{7,2}'
  SET  ARCDATE '&TXMDTUPD{7,4}&TXMDTUPD{1,2}&TXMDTUPD{4,2}'
ELSE
  SET  ARCDATE '00000000'    ;DEFAULT ARCHIVE DATE
ENDIF
IFTHEN ARCDATE,EQ,'00000000' ;IF ARCHIVE DATE IS ZERO
  SET  ARCTIME '&TXMTMCRE{1,2}&TXMTMCRE{4,2}&TXMTMCRE{7,2}'
  SET  ARCDATE '&TXMDTCRE{7,4}&TXMDTCRE{1,2}&TXMDTCRE{4,2}'
ENDIF
IFTHEN ARCTIME,EQ,'404040'   ;IF INVALID ARCHIVE TIME
  SET  ARCTIME '000000'
ENDIF

AUTHORIZ ON                  ;ALLOW ACCESS TO DOWNLOAD LIBRARY

IFTHEN REF,NE,''             ;IF REFERENCE MEMBER PROVIDED
  SET  PPDCOND,1             ;GET REFERENCE MEMBER ATTRIBUTES
  READ &REF
  IFTHEN SIBRETCD,NE,OK      ;IF NOT FOUND
    BYPASS                   ;MOVE MEMBER AS-IS TO DOWNLOAD LIBRARY
    RENAME &LIBNAM..&MEMNAM DOWNLOAD.&ARCMEM
  ELSE
    SET  ARCMEM  &TXMMEM     ;SET UP NEW MEMBER ATTRIBUTES
    SET  ARCAUD  &TXMMEM._AUD
    SET  MEMTYP  &TXMTYPE
    SET  PPDLITEX 1
    SET  MEMTTL '&TXMTITLE'
    BYPASS                   ;MOVE MEMBER TO DOWNLOAD LIBRARY
    RENAME &LIBNAM..&MEMNAM DOWNLOAD.&ARCMEM
    BYPASS                   ;APPLY NEW MEMBER ATTRIBUTES
    ALTER DOWNLOAD.&ARCMEM,TYPE=&MEMTYP,TITLE='&MEMTTL'
  ENDIF
  SET  PPDCOND,0
ELSE
  IFTHEN NAME,NE,''          ;IF AN OVERRIDE NAME IS PROVIDED
    SET  ARCMEM  &NAME
    SET  ARCAUD  &NAME._AUD
  ENDIF
  BYPASS
  RENAME &LIBNAM..&MEMNAM DOWNLOAD.&ARCMEM
  IFTHEN TYPE,NE,''          ;IF AN OVERRIDE TYPE IS PROVIDED
    BYPASS
    ALTER DOWNLOAD.&ARCMEM,TYPE=&TYPE
  ENDIF
  IFTHEN TITLE,NE,''         ;IF AN OVERRIDE TITLE IS PROVIDED
    SET  PPDLITEX 1
    SET  MEMTTL '&TITLE'
    BYPASS
    ALTER DOWNLOAD.&ARCMEM,TITLE='&MEMTTL'
  ENDIF
ENDIF

BLOCK                        ;CONVERT DASHES TO UNDERSCORES
  SET  PPDCOND  1
  VEXAM ARCMEM '-'
  IF SIBRETCD,NE,OK          ;EXIT IF NO DASHES IN MEMBER NAME
    LEAVE
  SET  PPDCOND  0
  SET  LEN     &PPDCOL
  SUBT LEN     1
  SET  ARCTMP  &ARCMEM{1,&LEN}_
  ADD  LEN     2
  SET  ARCTMP  &ARCTMP.&ARCMEM{&LEN,}
  BYPASS                     ;RENAME DOWNLOAD MEMBER TO NEW NAME
  RENAME DOWNLOAD.&ARCMEM DOWNLOAD.&ARCTMP
  SET  ARCMEM  &ARCTMP       ;KEEP MEMBER ATTRIBUTES IN SYNC
  SET  ARCAUD  &ARCTMP._AUD
  LOOP
ENDBLOCK

SET  PPDCOND  1              ;SLASHES WON'T WORK ON NETWORK
VEXAM TXMTYPE '\/|\\'
IFTHEN SIBRETCD,EQ,OK
  SET  PPDCOND  0
  SET  MEMTYP  'TEXT'
  ALTER DOWNLOAD.&ARCMEM,TYPE=&MEMTYP
ENDIF
SET  PPDCOND  0

IFTHEN TXMTYPE,EQ,''         ;BLANK FILE TYPE NOT ALLOWED
  SET  MEMTYP  'TEXT'
  ALTER DOWNLOAD.&ARCMEM,TYPE=&MEMTYP
ENDIF

STACK OPT=CLEAR              ;START WITH A CLEAR STACK

BYPASS
LIST DOWNLOAD.&ARCMEM        ;PUT ANY AUDIT TRAIL ONTO STACK
SET  PPDCOND  1
SCREEN AUD
IFTHEN SIBRETCD,EQ,OK
  BYPASS
  STACK *
ENDIF
SET  PPDCOND  0
BYPASS
END

IFTHEN SIBSTSIZ,GT,0         ;IF AN AUDIT TRAIL WAS FOUND
  BYPASS                     ;CREATE A MEMBER TO HOLD AUDIT TRAIL
  DEFINE DOWNLOAD.&ARCAUD,TEXT,'&ARCMEM AUDIT SUMMARY'
  BYPASS                     ;EDIT THE AUDIT MEMBER
  EDIT DOWNLOAD.&ARCAUD
  BYPASS                     ;RETRIEVE THE CONTENTS OF THE STACK
  GET $STACK
  BYPASS                     ;SAVE THE AUDIT MEMBER
  SAVE
  BYPASS                     ;CLEAR ANY AUDIT TRAIL IN AUDIT MEMBER
  ALTER DOWNLOAD.&ARCAUD,AUDIT=OFF,STAMP=OFF
ENDIF

BYPASS                       ;CLEAR ANY AUDIT TRAIL IN BASE MEMBER
ALTER DOWNLOAD.&ARCMEM,AUDIT=OFF,STAMP=OFF

AUTHORIZ OFF                 ;DENY ACCESS TO DOWNLOAD LIBRARY

SET  PPDTRAP  1

SUBMITF                      ;SUBMIT ARCHIVE JOB
* $$ JOB JNM=ARCHIVE,CLASS=0,DISP=D,USER=&SIBUSER
* $$ LST LST=SYSLST,CLASS=Z,DISP=D
// JOB    ARCHIVE   &SIBUSER   &LIBNAM..&MEMNAM
// LIBDEF *,SEARCH=(PRD2.CONFIG,ESP.BSI),TEMP
/. STEP01 - DOWNLOAD BIM-EDIT ARCHIVE MEMBERS (BSTTFTPC)
// EXEC   PGM=BSTTFTPC,SIZE=(*,512K)
* $$ SLI MEM=DAPFTP.B,S=PRD2.CONFIG
CWD \ITMainframe\Archives\BimEdit
TYPE A
CRLF ON
INPUT EXIT BSTTBEAM BIMEDIT $BAT BATCH DOWNLOAD &ARCMEM
STOR &ARCMEM._&ARCDATE._&ARCTIME._&TXMTYPE..TXT
)IFTHEN SIBSTSIZ,GT,0        ;IF AN AUDIT TRAIL WAS FOUND
INPUT EXIT BSTTBEAM BIMEDIT $BAT BATCH DOWNLOAD &ARCAUD
STOR &ARCMEM._&ARCDATE._&ARCTIME._AUDIT.TXT
)ENDIF
QUIT
/* EOD
// SETPARM LRC=$RC
*  STEP01 RC=&LRC
// IF     $RC GT 0 THEN
*  DOWNLOAD FAILURE - FLUSH ON HOLD, FIX ISSUE, AND RERUN
// IF     $RC GT 0 THEN
// GOTO   NOTIFY
/. STEP02 - PURGE ARCHIVE MEMBERS FROM BIM-EDIT (BSTTFTPC)
// EXEC   PGM=BSTTFTPC,SIZE=(*,512K)
* $$ SLI MEM=VSEFTPT.B,S=PRD2.CONFIG
SMNT BSTTBEAM BIMEDIT $BAT BATCH
CWD DOWNLOAD
)IFTHEN SIBSTSIZ,GT,0        ;IF AN AUDIT TRAIL WAS FOUND
DELE &ARCAUD
)ENDIF
DELE &ARCMEM
QUIT
/* EOD
// SETPARM LRC=$RC
*  STEP02 RC=&LRC
// IF     $RC GT 0 THEN
*  DOWLOAD COMPLETE - MANUAL PURGE OF BIM-EDIT MEMBER(S) REQUIRED
// IF     $RC GT 0 THEN
// GOTO   NOTIFY
// GOTO   EXIT
/. NOTIFY
// PAUSE  IMPORTANT!!!  FOLLOW INSTRUCTIONS IN PREVIOUS MESSAGE
/. EXIT
/& EOJ
* $$ EOJ
++/*

EXIT OK,'(ARCHIVE) ** &LIBNAM..&MEMNAM ARCHIVED TO NETWORK **'
